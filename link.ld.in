/* # Developer notes

- Symbols that start with a double underscore (__) are considered "private"

- Symbols that start with a single underscore (_) are considered "semi-public"; they can be
  overridden in a user linker script, but should not be referred from user code (e.g. `extern "C" {
  static mut __sbss }`).

- `EXTERN` forces the linker to keep a symbol in the final binary. We use this to make sure a
  symbol if not dropped if it appears in or near the front of the linker arguments and "it's not
  needed" by any of the preceding objects (linker arguments)

- `PROVIDE` is used to provide default values that can be overridden by a user linker script

- On alignment: it's important for correctness that the VMA boundaries of both .bss and .data *and*
  the LMA of .data are all 4-byte aligned. These alignments are assumed by the RAM initialization
  routine. There's also a second benefit: 4-byte aligned boundaries means that you won't see
  "Address (..) is out of bounds" in the disassembly produced by `objdump`.
*/

/* Provides information about the memory layout of the device */
/* This will be provided by the user (see `memory.x`) or by a Board Support Crate */
INCLUDE memory.x

/* # Entry point = reset vector */
ENTRY(Reset);
EXTERN(__RESET_VECTOR); /* depends on the `Reset` symbol */

/* # Exception vectors */
/* This is effectively weak aliasing at the linker level */
/* The user can override any of these aliases by defining the corresponding symbol themselves (cf.
   the `exception!` macro) */
EXTERN(__EXCEPTIONS); /* depends on all the these PROVIDED symbols */

EXTERN(DefaultHandler);

PROVIDE(NonMaskableInt = DefaultHandler);
EXTERN(HardFaultTrampoline);
PROVIDE(MemoryManagement = DefaultHandler);
PROVIDE(BusFault = DefaultHandler);
PROVIDE(UsageFault = DefaultHandler);
PROVIDE(SecureFault = DefaultHandler);
PROVIDE(SVCall = DefaultHandler);
PROVIDE(DebugMonitor = DefaultHandler);
PROVIDE(PendSV = DefaultHandler);
PROVIDE(SysTick = DefaultHandler);

PROVIDE(DefaultHandler = DefaultHandler_);
PROVIDE(HardFault = HardFault_);

PROVIDE(ADC = DefaultHandler);
PROVIDE(CAN1_RX0 = DefaultHandler);
PROVIDE(CAN1_RX1 = DefaultHandler);
PROVIDE(CAN1_SCE = DefaultHandler);
PROVIDE(CAN1_TX = DefaultHandler);
PROVIDE(CAN2_RX0 = DefaultHandler);
PROVIDE(CAN2_RX1 = DefaultHandler);
PROVIDE(CAN2_SCE = DefaultHandler);
PROVIDE(CAN2_TX = DefaultHandler);
PROVIDE(CAN3_RX0 = DefaultHandler);
PROVIDE(CAN3_RX1 = DefaultHandler);
PROVIDE(CAN3_SCE = DefaultHandler);
PROVIDE(CAN3_TX = DefaultHandler);
PROVIDE(CRYP = DefaultHandler);
PROVIDE(CRYPTO = DefaultHandler);
PROVIDE(DCMI = DefaultHandler);
PROVIDE(DFSDM1_FLT0 = DefaultHandler);
PROVIDE(DFSDM1_FLT1 = DefaultHandler);
PROVIDE(DFSDM2_FILTER1 = DefaultHandler);
PROVIDE(DFSDM2_FILTER2 = DefaultHandler);
PROVIDE(DFSDM2_FILTER3 = DefaultHandler);
PROVIDE(DFSDM2_FILTER4 = DefaultHandler);
PROVIDE(DMA1_STREAM0 = DefaultHandler);
PROVIDE(DMA1_STREAM1 = DefaultHandler);
PROVIDE(DMA1_STREAM2 = DefaultHandler);
PROVIDE(DMA1_STREAM3 = DefaultHandler);
PROVIDE(DMA1_STREAM4 = DefaultHandler);
PROVIDE(DMA1_STREAM5 = DefaultHandler);
PROVIDE(DMA1_STREAM6 = DefaultHandler);
PROVIDE(DMA1_STREAM7 = DefaultHandler);
PROVIDE(DMA2D = DefaultHandler);
PROVIDE(DMA2_STREAM0 = DefaultHandler);
PROVIDE(DMA2_STREAM1 = DefaultHandler);
PROVIDE(DMA2_STREAM2 = DefaultHandler);
PROVIDE(DMA2_STREAM3 = DefaultHandler);
PROVIDE(DMA2_STREAM4 = DefaultHandler);
PROVIDE(DMA2_STREAM5 = DefaultHandler);
PROVIDE(DMA2_STREAM6 = DefaultHandler);
PROVIDE(DMA2_STREAM7 = DefaultHandler);
PROVIDE(DSIHOST = DefaultHandler);
PROVIDE(ETH = DefaultHandler);
PROVIDE(ETH_WKUP = DefaultHandler);
PROVIDE(EXTI0 = DefaultHandler);
PROVIDE(EXTI1 = DefaultHandler);
PROVIDE(EXTI15_10 = DefaultHandler);
PROVIDE(EXTI17_RTC_ALARM = DefaultHandler);
PROVIDE(EXTI19 = DefaultHandler);
PROVIDE(EXTI2 = DefaultHandler);
PROVIDE(EXTI20 = DefaultHandler);
PROVIDE(EXTI3 = DefaultHandler);
PROVIDE(EXTI4 = DefaultHandler);
PROVIDE(EXTI9_5 = DefaultHandler);
PROVIDE(FLASH = DefaultHandler);
PROVIDE(FMC = DefaultHandler);
PROVIDE(FPU = DefaultHandler);
PROVIDE(FSMC = DefaultHandler);
PROVIDE(HASH_RNG = DefaultHandler);
PROVIDE(I2C1_ER = DefaultHandler);
PROVIDE(I2C1_ERR = DefaultHandler);
PROVIDE(I2C1_EV = DefaultHandler);
PROVIDE(I2C1_EVT = DefaultHandler);
PROVIDE(I2C2_ER = DefaultHandler);
PROVIDE(I2C2_ERR = DefaultHandler);
PROVIDE(I2C2_EV = DefaultHandler);
PROVIDE(I2C2_EVT = DefaultHandler);
PROVIDE(I2C3_ER = DefaultHandler);
PROVIDE(I2C3_EV = DefaultHandler);
PROVIDE(I2C4_ER = DefaultHandler);
PROVIDE(I2C4_EV = DefaultHandler);
PROVIDE(I2CFMP1ERROR = DefaultHandler);
PROVIDE(I2CFMP1EVENT = DefaultHandler);
PROVIDE(I2CFMP1_ERROR = DefaultHandler);
PROVIDE(I2CFMP1_EVENT = DefaultHandler);
PROVIDE(LCD_TFT = DefaultHandler);
PROVIDE(LCD_TFT_1 = DefaultHandler);
PROVIDE(LPTIM1 = DefaultHandler);
PROVIDE(LPTIM1_OR_IT_EIT_23 = DefaultHandler);
PROVIDE(LTDC = DefaultHandler);
PROVIDE(LTDC_ER = DefaultHandler);
PROVIDE(OTG_FS = DefaultHandler);
PROVIDE(OTG_FS_WKUP = DefaultHandler);
PROVIDE(OTG_HS = DefaultHandler);
PROVIDE(OTG_HS_EP1_IN = DefaultHandler);
PROVIDE(OTG_HS_EP1_OUT = DefaultHandler);
PROVIDE(OTG_HS_WKUP = DefaultHandler);
PROVIDE(PVD = DefaultHandler);
PROVIDE(PWM1_UP = DefaultHandler);
PROVIDE(QUADSPI = DefaultHandler);
PROVIDE(QUAD_SPI = DefaultHandler);
PROVIDE(RCC = DefaultHandler);
PROVIDE(RNG = DefaultHandler);
PROVIDE(RTC_ALARM = DefaultHandler);
PROVIDE(RTC_WKUP = DefaultHandler);
PROVIDE(SAI1 = DefaultHandler);
PROVIDE(SDIO = DefaultHandler);
PROVIDE(SPI1 = DefaultHandler);
PROVIDE(SPI2 = DefaultHandler);
PROVIDE(SPI3 = DefaultHandler);
PROVIDE(SPI4 = DefaultHandler);
PROVIDE(SPI5 = DefaultHandler);
PROVIDE(SPI6 = DefaultHandler);
PROVIDE(TAMP_STAMP = DefaultHandler);
PROVIDE(TIM12 = DefaultHandler);
PROVIDE(TIM13 = DefaultHandler);
PROVIDE(TIM14 = DefaultHandler);
PROVIDE(TIM1_BRK_TIM9 = DefaultHandler);
PROVIDE(TIM1_CC = DefaultHandler);
PROVIDE(TIM1_TRG_COM_TIM11 = DefaultHandler);
PROVIDE(TIM1_UP_TIM10 = DefaultHandler);
PROVIDE(TIM2 = DefaultHandler);
PROVIDE(TIM3 = DefaultHandler);
PROVIDE(TIM4 = DefaultHandler);
PROVIDE(TIM5 = DefaultHandler);
PROVIDE(TIM6_DAC = DefaultHandler);
PROVIDE(TIM6_DAC1 = DefaultHandler);
PROVIDE(TIM6_DACUNDER = DefaultHandler);
PROVIDE(TIM6_GLB_IT_DAC1_DAC2 = DefaultHandler);
PROVIDE(TIM7 = DefaultHandler);
PROVIDE(TIM8_BRK_TIM12 = DefaultHandler);
PROVIDE(TIM8_CC = DefaultHandler);
PROVIDE(TIM8_TRG_COM_TIM14 = DefaultHandler);
PROVIDE(TIM8_UP_TIM13 = DefaultHandler);
PROVIDE(UART10 = DefaultHandler);
PROVIDE(UART4 = DefaultHandler);
PROVIDE(UART5 = DefaultHandler);
PROVIDE(UART7 = DefaultHandler);
PROVIDE(UART8 = DefaultHandler);
PROVIDE(UART9 = DefaultHandler);
PROVIDE(USART1 = DefaultHandler);
PROVIDE(USART2 = DefaultHandler);
PROVIDE(USART3 = DefaultHandler);
PROVIDE(USART4 = DefaultHandler);
PROVIDE(USART6 = DefaultHandler);
PROVIDE(USART7 = DefaultHandler);
PROVIDE(USART8 = DefaultHandler);
PROVIDE(WWDG = DefaultHandler);




/* # Interrupt vectors */
EXTERN(__INTERRUPTS); /* `static` variable similar to `__EXCEPTIONS` */

/* # Pre-initialization function */
/* If the user overrides this using the `pre_init!` macro or by creating a `__pre_init` function,
   then the function this points to will be called before the RAM is initialized. */
PROVIDE(__pre_init = DefaultPreInit);

/* # Sections */
SECTIONS
{
  PROVIDE(_stack_start = 0x20004000);

  /* ## Sections in FLASH */
  /* ### Vector table */
  .vector_table ORIGIN(FLASH) :
  {
    /* Initial Stack Pointer (SP) value */
    LONG(_stack_start);

    /* Reset vector */
    KEEP(*(.vector_table.reset_vector)); /* this is the `__RESET_VECTOR` symbol */
    __reset_vector = .;

    /* Exceptions */
    KEEP(*(.vector_table.exceptions)); /* this is the `__EXCEPTIONS` symbol */
    __eexceptions = .;

    /* Device specific interrupts */
    KEEP(*(.vector_table.interrupts)); /* this is the `__INTERRUPTS` symbol */
  } > FLASH

  PROVIDE(_stext = ADDR(.vector_table) + SIZEOF(.vector_table));

  /* ### .text */
  .text _stext :
  {
    /* place these 2 close to each other or the `b` instruction will fail to link */
    *(.PreResetTrampoline);
    *(.Reset);

    *(.text .text.*);
    *(.HardFaultTrampoline);
    *(.HardFault.*);
    . = ALIGN(16);
    __etext = .;
  } > FLASH

  /* ### .rodata */
  .rodata __etext : ALIGN(16)
  {
    *(.rodata .rodata.*);

    /* 4-byte align the end (VMA) of this section.
       This is required by LLD to ensure the LMA of the following .data
       section will have the correct alignment. */
    . = ALIGN(4);
    __erodata = .;
  } > FLASH

  /* ### .ARM.exidx */
  .ARM.exidx __erodata : ALIGN(4)
  {
    . = ALIGN(4);
    __sarm_exidx = .;

    *(.ARM.exidx .ARM.exidx.*);

    /* Not sure if we need the below line. */
    /* *(.ARM.exidx.*); */

    /* 4-byte align the end (VMA) of this section.
       This is required by LLD to ensure the LMA of the following .data
       section will have the correct alignment. */
    . = ALIGN(4);
    __earm_exidx = .;
  } > FLASH

  /* ### .ARM.extab */
  .ARM.extab __earm_exidx : ALIGN(4)
  {
    . = ALIGN(4);
    __sarm_extab = .;
    *(.ARM.extab .ARM.extab.*);

    /* 4-byte align the end (VMA) of this section.
       This is required by LLD to ensure the LMA of the following .data
       section will have the correct alignment. */
    . = ALIGN(4);
    __earm_extab = .;
  } > FLASH

  /* ## Sections in RAM */
  /* ### .data */
  .data : ALIGN(4)
  {
    . = ALIGN(4);
    __sdata = .;
    *(.data .data.*);
    . = ALIGN(4); /* 4-byte align the end (VMA) of this section */
    __edata = .;
  } > RAM AT>FLASH

  /* LMA of .data */
  __sidata = LOADADDR(.data);

  /* ### .bss */
  .bss (NOLOAD) : ALIGN(4)
  {
    . = ALIGN(4);
    __sbss = .;
    *(.bss .bss.*);
    . = ALIGN(4); /* 4-byte align the end (VMA) of this section */
    __ebss = .;
  } > RAM AT>FLASH

  /* ### .uninit */
  /* .uninit (NOLOAD) : ALIGN(4)
  {
    . = ALIGN(4);
    *(.uninit .uninit.*);
    . = ALIGN(4);
  } > RAM */

  /* Place the heap right after `.uninit` */
  . = ALIGN(4);
  __sheap = .;

  /* ## .got */
  /* Dynamic relocations are unsupported. This section is only used to detect relocatable code in
     the input files and raise an error if relocatable code is found */
  .got : ALIGN(4)
  {
    KEEP(*(.got .got.*));
  }

  /* ## Discarded sections */
  /DISCARD/ :
  {
    /* Unused exception related info that only wastes space */
    /* *(.ARM.exidx);
    *(.ARM.exidx.*);
    *(.ARM.extab.*); */
  }
}

/* Do not exceed this mark in the error messages below                                    | */
/* # Alignment checks */
ASSERT(ORIGIN(FLASH) % 4 == 0, "
ERROR(cortex-m-rt): the start of the FLASH region must be 4-byte aligned");

ASSERT(ORIGIN(RAM) % 4 == 0, "
ERROR(cortex-m-rt): the start of the RAM region must be 4-byte aligned");

ASSERT(__sdata % 4 == 0 && __edata % 4 == 0, "
BUG(cortex-m-rt): .data is not 4-byte aligned");

ASSERT(__sidata % 4 == 0, "
BUG(cortex-m-rt): the LMA of .data is not 4-byte aligned");

ASSERT(__sbss % 4 == 0 && __ebss % 4 == 0, "
BUG(cortex-m-rt): .bss is not 4-byte aligned");

ASSERT(__sheap % 4 == 0, "
BUG(cortex-m-rt): start of .heap is not 4-byte aligned");

/* # Position checks */

/* ## .vector_table */
ASSERT(__reset_vector == ADDR(.vector_table) + 0x8, "
BUG(cortex-m-rt): the reset vector is missing");

ASSERT(__eexceptions == ADDR(.vector_table) + 0x40, "
BUG(cortex-m-rt): the exception vectors are missing");

ASSERT(SIZEOF(.vector_table) > 0x40, "
ERROR(cortex-m-rt): The interrupt vectors are missing.
Possible solutions, from most likely to less likely:
- Link to a svd2rust generated device crate
- Disable the 'device' feature of cortex-m-rt to build a generic application (a dependency
may be enabling it)
- Supply the interrupt handlers yourself. Check the documentation for details.");

/* ## .text */
ASSERT(ADDR(.vector_table) + SIZEOF(.vector_table) <= _stext, "
ERROR(cortex-m-rt): The .text section can't be placed inside the .vector_table section
Set _stext to an address greater than the end of .vector_table (See output of `nm`)");

ASSERT(_stext + SIZEOF(.text) < ORIGIN(FLASH) + LENGTH(FLASH), "
ERROR(cortex-m-rt): The .text section must be placed inside the FLASH memory.
Set _stext to an address smaller than 'ORIGIN(FLASH) + LENGTH(FLASH)'");

/* # Other checks */
ASSERT(SIZEOF(.got) == 0, "
ERROR(cortex-m-rt): .got section detected in the input object files
Dynamic relocations are not supported. If you are linking to C code compiled using
the 'cc' crate then modify your build script to compile the C code _without_
the -fPIC flag. See the documentation of the `cc::Build.pic` method for details.");
/* Do not exceed this mark in the error messages above                                    | */

ASSERT(SIZEOF(.vector_table) <= 0x400, "
There can't be more than 240 interrupt handlers. This may be a bug in
your device crate, or you may have registered more than 240 interrupt
handlers.");
